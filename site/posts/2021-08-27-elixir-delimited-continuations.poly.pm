#lang pollen

â—Š(define-meta title "Delimited Continuations with shift/reset in Elixir")
â—Š(define-meta date "2021-08-27")
â—Š(define-meta tags ("elixir" "delimited continuations" "proof of concept"))

â—Šul{
â—Šli{â—Šhyperlink["#sec:introduction"]{Introduction}}
â—Šli{â—Šhyperlink["#sec:examples"]{Examples}}
â—Šli{â—Šhyperlink["#sec:limitations"]{Limitations}}
â—Šli{â—Šhyperlink["#sec:references-and-further-resources"]{References and further resources}}
â—Šli{â—Šhyperlink["#sec:appendix"]{Appendix}}
â—Šli{â—Šhyperlink["#sec:footnotes"]{Footnotes}}
}



â—Šh1[#:id "sec:introduction"]{Introduction}

â—Šp{Ever since I heard about those things called "continuations", I've
been fascinated by what they are and how they work, specially after
discovering this thing called â—Šcode{callCC}â—Šfootnote{call-cc-wiki} â—Šfootnote{scheme-call-cc-wiki}. If this was not
hard enough to grasp, some time later I discovered that continuations
can be â—Šemph{delimited} or â—Šemph{undelimited}. Recently I found several
references about continuationsâ—Šfootnote{continuation-study-group-gh}, but they tend to be papers that
are hard to read without some prior knowledge, which I don't
have. Therefore, I'm unable to present here some intuitive explanation
about them. Maybe after much more reading.}

â—Šp{Instead, this post is about a proof of concept (POC) delimited
continuations library in Elixir. This is the result of my attempts at
trying to understand (delimited) continuations by way of practice. The
resulting library converts Elixir code into continuation passing
style, and then uses that transformed code to implement the control
operators â—Šcode{shift} and â—Šcode{reset}. Theses things will be briefly discussed
here. I hope this may help someone else trying to grasp continuations
and, if I'm lucky, someone may teach me more by pointing out the gaps
in my understanding and implementation. =)}

â—Šp{This post presents some (hopefully) interesting examples of what can
be done using those operators. A very simplistic (and perhaps
erroneous, as I'm still grasping those conceptsâ€¦ beware!)
explanation of what I gathered so far about this topic and a few
implementation details I stumbled upon are presented in the
â—Šhyperlink["#sec:appendix"]{appendix}. The implementation is available on â—Šhyperlink["https://github.com/thalesmg/campinas/tree/2d41f2697395c2fc542500ad32c62f0f307c7220"]{Github}.}

â—Šh1[#:id "sec:examples"]{Examples}

â—Šp{Here, I'll present the basic usage of the library and some
examples. For more on â—Šhyperlink["#sec:cps"]{CPS} and â—Šhyperlink["sec:shift---reset"]{â—Šcode{shift/reset}}, see the corresponding
sections. Several examples will slowly build over one another because
I found it interesting to do so when I was trying to grasp the
concepts myself gradually.}

â—Šp{The experimental library is Campinasâ—Šfootnote{campinas-name}, available on â—Šhyperlink["https://github.com/thalesmg/campinas/tree/2d41f2697395c2fc542500ad32c62f0f307c7220"]{Github}. To
use it, just add â—Šcode{use Campinas} at the beginning of your module,
define functions to be CPSed using the â—Šcode{defcps/2} macro and run them
using the â—Šcode{runCPS/1} macro. Notice that the current implementation is
pretty much a POC, so several AST nodes are not yet supported, such as
pipes, â—Šcode{for/1}, â—Šcode{receive/after} blocks and many others.}

â—Šp{The most basic example is one where nothing fancy is done:}

â—Šhighlight['elixir]{
defmodule Example do
  use Campinas

  defcps ex1(x) do
    y = x + 1
    z = 2 * y - 3
    div(z, 3)
  end
end
}

â—Šp{Running â—Šcode{runCPS(Example.ex1(10))} gives â—Šcode{6}, as expected.}

â—Šp{Things start to get a bit more interesting when we introduce
â—Šcode{shift/reset}. I'll give a few very basic examples with growing
complexity to try to demonstrate how those work intuitively. First,
using just â—Šcode{reset} basically does nothing by itself:}

â—Šhighlight['elixir]{
defcps ex2() do
  x = reset do
        4
      end
  x + 1
end
}

â—Šp{â—Šcode{runCPS(Example.ex2())} results in â—Šcode{5}, as if â—Šcode{reset} wasn't
there. Similarly, an expression using only â—Šcode{shift} appears to have no
visible effects as well (â—Šcode{cont} is the name we give our continuation;
it can be something else as well):}

â—Šhighlight['elixir]{
defcps ex3() do
  shift cont do
    6
  end
end
}

â—Šp{â—Šcode{runCPS(Example.ex3())} indeed results in â—Šcode{6}. The first strange
example is one where we use â—Šcode{shift} in a more complex
expression. Let's take â—Šcode{ex1} and use â—Šcode{shift} in its second expression: â—Šspan[#:id "example_ex4"]{}}

â—Šhighlight['elixir]{
defcps ex4(x) do
  y = x + 1
  z = 2 * shift cont do
            99
          end - 3
  div(z, 3)
end
}

â—Šp{If â—Šcode{shift} were truly a no-op, one would expect
â—Šcode{runCPS(Example.ex4(10))} to yield â—Šcode{65}. Yet, the final result is
â—Šcode{99}! It is as if invoking â—Šcode{shift} acts like an early function exit
such as â—Šcode{return} from a more imperative language. Notice that we
didn't invoke the continuation inside â—Šcode{shift}. If we do that:}

â—Šhighlight['elixir]{
defcps ex5(x) do
  y = x + 1
  z = 2 * shift k do  # also changed the name, for fun
            k(99)     # here we invoke the captured continuation
          end - 3
  div(z, 3)
end
}

â—Šp{... then, indeed, â—Šcode{runCPS(Example.ex5(10))} yields â—Šcode{65}. But what if
we do more than simply invoking the continuation inside â—Šcode{shift}?}

â—Šhighlight['elixir]{
defcps ex6(x) do
  y = x + 1
  z = 2 * shift k do
            k(99) - 60
          end - 3
  div(z, 3)
end
}

â—Šp{This results in â—Šcode{5}! In effect, it is the same as â—Šcode{ex4}, but with â—Šcode{60}
subtracted from it. Essentially, when we invoke â—Šcode{shift}, the whole
function result is â—Šcode{shift}'s body. In this example, the continuation
is a function that is roughly equivalent to â—Šcode{fn v -> div(2 * v - 3, 3)
end}.}

â—Šp{That behavior is changed when we add â—Šcode{reset} to the mix. It
effectively â—Šemph{delimits} the extent to which â—Šcode{shift} can capture the
continuation.}

â—Šhighlight['elixir]{
defcps ex7(x) do
  y = x + 1
  z = reset do
        2 * shift cont do
              cont(99) - 60
            end
      end - 3
  div(z, 3)
end
}

â—Šp{The result now is â—Šcode{45}. The captured continuation is now equivalent to
â—Šcode{fn v -> 2 * v end} because of the enclosing â—Šcode{reset}. Another
interesting thing is that the continuation may be invoke more than
once. The following example results in â—Šcode{13}:}

â—Šhighlight['elixir]{
defcps ex8() do
  z = reset do
        2 * shift cont do
              cont(cont(8)) + 11
            end
      end - 3
  div(z, 3)
end
}

â—Šp{Multiple CPSed functions that use â—Šcode{shift} and â—Šcode{reset} can be composed
using the â—Šemph{ad-hoc} syntax â—Šcode{@[expression]}. We can break up â—Šcode{ex8} into
two smaller functions, and has the same behavior as before:}

â—Šhighlight['elixir]{
defcps ex9() do
  shift cont do
    cont(cont(8)) + 11
  end
end

defcps ex10() do
  z = reset do
        2 * @[ex9()]
      end - 3
  div(z, 3)
end
}

â—Šp{We can also â—Šcode{shift} inside of a tuple literal:}

â—Šhighlight['elixir]{
defcps ex11() do
  {
    1,
    2,
    shift cont do
      Tuple.to_list(cont(99))
    end,
    4
  }
end
}

â—Šp{This results in â—Šcode{[1, 2, 99, 4]}.}

â—Šp{A more bizarre example taken from one of the original papers that
introduce â—Šcode{shift/reset}â—Šfootnote{abstracting-control-danvy-filinski} is one that writes seemingly direct code
to compute "non-deterministically" combinations of numbers that add up
to a given value. We need to define a few functions that do the magic:}

â—Šhighlight['elixir]{
@doc "aborts the computation early"
defcps fail() do
  shift k do
    :no
  end
end

@doc "tries to continue with both true and false"
defcps flip() do
  shift k do
    k(true)
    k(false)
    @[fail()]
  end
end

@doc "continues with all numbers below a maximum"
defcps choice(n) do
  # by the way, `if` is supported
  if n < 1 do
    @[fail()]
  else
    if @[flip()] do
      @[choice(n - 1)]
    else
      n
    end
  end
end

@doc """
tries to find all x > y > z with x <= n such that x + y + z = s
"""
defcps triples(n, s) do
  x = @[choice(n)]
  y = @[choice(x - 1)]
  z = @[choice(y - 1)]

  if x + y + z == s do
    send(self(), {:found, {x, y, z}})
  else
    @[fail()]
  end
end
}

â—Šp{Then, to find such triples with â—Šcode{n = 9} and â—Šcode{s = 15}:}

â—Šhighlight['elixir]{
defcps ex12() do
  @[triples(9, 15)]
end
}

â—Šp{Running this yields:}

â—Šhighlight['elixir]{
iex(201)> runCPS(Example.ex12())
:no
iex(202)> flush()
{:found, {6, 5, 4}}
{:found, {7, 5, 3}}
{:found, {7, 6, 2}}
{:found, {8, 4, 3}}
{:found, {8, 5, 2}}
{:found, {8, 6, 1}}
{:found, {9, 4, 2}}
{:found, {9, 5, 1}}
:ok
iex(203)>
}

â—Šh2[#:id "sec:effect-systems"]{Effect systems}

â—Šp{As the final example, I'll show simple error and state effects built
upon those operators. It has been observed that delimited
continuations can be used to model effect systemsâ—Šfootnote{ghc-continuation-primops} â—Šfootnote{algebraic-effects-handlers}.}

â—Šp{The simplest one is the error effect. Reminding ourselves of example
â—Šhyperlink["#example_ex4"]{â—Šcode{ex4}}, an early exit would be implemented as simply as calling
â—Šcode{shift}. To add extra spice, we'll consider â—Šemph{recoverable} exceptions:
the user provides a handler that receives the error â—Šcode{e} and decides if
computation should halt and return â—Šcode{{:error, e}}, or if it should
continue (and provide a value back to the computation).}

â—Šhighlight['elixir]{
# the program
defmodule ErrorExample do
  use Campinas
  alias Campinas.Effects.Error

  defcps program1(x) do
    y = x * x - 1

    if y < 0 do
      @[Error.error(:negative)]
    end

    result =
    if y > 100 do
      @[Error.error({:too_big, y})]
    else
      div(y, 2)
    end

    result - 1
  end
end

# the usage
handler = fn
  {:too_big, n} ->
    send(self(), {:big_number, n})

  if rem(n, 2) == 0 do
    {:cont, 0}
  else
    :halt
  end

  e ->
    send(self(), {:some_error, e})
  :halt
end

run_error(ErrorExample.program1(2), handler)
# should return `{:ok, 0}` without calling the handler

run_error(ErrorExample.program1(11), handler)
# should return `{:ok, -1}` and call the handler, which continues

run_error(ErrorExample.program1(0), handler)
# returns `{:error, :negative}` and call the handler, which aborts
}

â—Šp{The state effect is our last example. It has two operations: â—Šcode{get/0},
which simply reads the current state, and â—Šcode{set/1}, which defines a new
state. The stateful program is run by being fed to â—Šcode{run_state/2} along
with the initial state. This returns â—Šcode{{:ok, result, final_state}}.}

â—Šhighlight['elixir]{
# the program
defmodule StateExample do
  use Campinas
  alias Campinas.Effects.State

  defcps program1(x) do
    s1 = @[State.get()]
    s2 = x + s1

    if rem(s2, 2) == 0 do
      @[State.set(s2 + 11)]
    else
      @[State.set(4 * s2)]
    end

    2 * s2 + 1
  end
end

# the usage
run_state(StateCases.program1(11), 20)
# returns `{:ok, 63, 124}`; 63 is the result; 124 is the final state

run_state(StateCases.program1(10), 20)
# returns `{:ok, 61, 41}`; 61 is the result; 41 is the final state
}

â—Šp{Notice that there is no mutation involved, nor exceptions being
raised/thrown (in the Elixir/Erlang sense) in those examples. ;)}

â—Šh1[#:id "sec:limitations"]{Limitations}

â—Šp{I have not implemented several AST node possibilities in the
transformation, so almost anything outside the examples in the tests
will probably not work. =)}

â—Šp{The example from the composable-continuation tutorial on the Scheme
Wiki does not work with the current version. I believe that
â—Šcode{Enum.each} (the equivalent of â—Šcode{for-each} there) would need to be
CPSed for that to work.}

â—Šh1[#:id "sec:references-and-further-resources"]{References and further resources}

â—Šp{Here are some resources I have used, not necessarily in their
entirety, and others that I have found while researching this topic.}

â—Šul{
â—Šli{â—Šhyperlink["https://www.youtube.com/watch?v=QNM-njddhIw"]{Delimited Continuations for Everyone by Kenichi Asai (Youtube)}

Nice video explaining delimited continuations with examples. It is
also where I found some recommendations of further resources (around
01:30).}
â—Šli{â—Šhyperlink["https://www.cambridge.org/core/journals/mathematical-structures-in-computer-science/article/abs/representing-control-a-study-of-the-cps-transformation/37193FD94F87443338FC7F519783FF0A"]{Olivier Danvy and Andre Filinski, "Representing Control: a Study of the CPS Transformation", MSCS, 1992}

The introductory paper recommended by Kenichi Asai. It does seem to
have some prior knowledge assumptions, but seems very comprehensive
and has very helpful tables of conversion rules for CPSing programs.}
â—Šli{â—Šhyperlink["https://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.43.8753"]{Olivier Danvy and Andre Filinski, â€œAbstracting Control,â€ LISP and Functional Programming, 1990}

Another earlier paper by the authors who introduced â—Šcode{shift} and
â—Šcode{reset}. It is more compact, has a couple examples, but is much more
dense and harder to understand (much more assumed knowledge about
concepts and notation).}
â—Šli{â—Šhyperlink["https://docs.racket-lang.org/reference/cont.html"]{Racket Reference Manual on Continuations}

Great source of references and displays other control operators. Not
quite didactic, but I recommend browsing it and trying out the
operators, since the implementation is solid in Racket.}
â—Šli{â—Šhyperlink["http://community.schemewiki.org/?composable-continuations-tutorial"]{Composable Continuations Tutorial on Scheme Wiki}

A nice and short tutorial with some examples that are very
illuminating examples that are valuable to be worked out manually.}
â—Šli{â—Šhyperlink["https://github.com/swannodette/delimc"]{â—Šcode{delimc} by David Nolen}

A delimited continuations library for Clojure. Nice and short
implementation to study.}
â—Šli{â—Šhyperlink["https://github.com/ghc-proposals/ghc-proposals/blob/master/proposals/0313-delimited-continuation-primops.rst"]{The proposal to add delimited control primops to GHC} and â—Šhyperlink["https://mail.haskell.org/pipermail/ghc-devs/2020-July/019016.html"]{a companion email thread} â—Šspan[#:id "ref_ghc_primops_alexis"]{}

Low level discussion of adding control operators similar to â—Šcode{shift}
and â—Šcode{reset} to GHC, and how these affect the execution stack.}
â—Šli{â—Šhyperlink["https://stackoverflow.com/a/29838823/2708711"]{This answer to a StackOverflow question about continuation prompts by Alexis King} â—Šspan[#:id "ref_stackoverflow_alexis"]{}

Has some nice visualizations relating stack frames and delimited
continuations.}
â—Šli{â—Šhyperlink["https://wingolog.org/archives/2010/02/26/guile-and-delimited-continuations"]{guile and delimited continuations, by Andy Wingo} â—Šspan[#:id "ref_guile_wingo"]{}

One of the implementers of GNU Guile (a Scheme implementation)
discusses adding delimited continuations to the language. Has some
nice illustrations of the stack for the â—Šcode{control/prompt} operators
(cousins of â—Šcode{shift/reset}).}
â—Šli{â—Šhyperlink["https://github.com/rain-1/continuations-study-group"]{rain-1's continuation study group}

A vast collection of papers and references about continuations in
general. It'll take quite a while to chew through all that. =)

I'd love to know if this group has a forum or similar channel where
one could ask questions.}
}

â—Šh1[#:id "sec:appendix"]{Appendix}

â—Šh2[#:id "sec:continuation-passing-style--cps-"]{Continuation Passing Style (CPS)}

â—Šp{This sections describes briefly what CPS is and some decisions that I
had to make in the implementation in order for it to work. Although
I'm still making sense of them ğŸ™ˆ.}

â—Šp{A continuation is the materialization of "what comes next" at a given
point of execution of a program. Or, a continuation is the evaluation
context surrounding the â—Šemph{reducible expression}
(â—Šemph{redex})â—Šfootnote{racket-cont-model}. Using the same example
from the Racket documentation:}

â—Šhighlight['elixir]{
#  continuation
# â†“â†“â†“â†“
  4 - (1 + 1)
#     â†‘â†‘â†‘â†‘â†‘â†‘â†‘
#      redex
}

â—Šp{Here, in order for the whole expression to be reduced, the redex is
â—Šcode{(1 + 1)}, and the continuation is â—Šcode{4 - _}, where â—Šcode{_} takes the place
of the redex as it is reduced. As another example:}

â—Šhighlight['elixir]{
def some_fun() do
  x = 1      # the lines below are this expression's continuation.
  y = x + 2  # `x` is the "redex" that is fed here, and `_ + 3` is
             # this line's continuation.
  y + 3      # within this line, `_ + 3` is `y`'s continuation
end
}

â—Šp{â—Šemph{Continuation Passing Style (CPS)} is a way of writing functions and
expressions where the continuation is passed as an explicit argument
to the redex.}

â—Šh3[#:id "sec:irreducible-values"]{Irreducible values}

â—Šp{The simplest case is that of a value that cannot be reduced
further. Using the
notationâ—Šfootnote{representing-control-danvy-filinski} â—Šcode{[[ E
]]} to denote the CPS conversion of a term â—Šcode{E}, the conversion of
a pure value is simply:}

â—Šhighlight['racket]{
[[ x ]] = Î»Îº. Îº x
}

â—Šp{In Elixir:}

â—Šhighlight['elixir]{
# a simple value...
1
# ... in CPS form becomes:
fn k -> # `k` is the continuation, to be provided by some other code.
  k.(1) # that continuation is invoked and receives the value to
        # proceed.
end
}

â—Šh3[#:id "sec:primitive-function-application"]{Primitive function application}

â—Šp{Another simple case is that of primitive function application. A
primitive function is one that is considered a "black box" and its
definition cannot be directly converted into CPS. I considered local
and remote function calls as primitives.}

â—Šp{For a primitive function â—Šcode{p} applied to â—Šcode{x}, its conversion rule is:}

â—Šhighlight['racket]{
[[ p x ]] = Î»Îº. [[ x ]] (Î»a. Îº (p a))
}

â—Šp{Let's take as an example the negate unary operator, â—Šhyperlink["https://hexdocs.pm/elixir/Kernel.html#-/1"]{â—Šcode{Kernel.-/1}}.}

â—Šhighlight['elixir]{
- x
# ... in CPS form becomes:
fn k1 ->         # the outer continuation
  (fn k2 ->      # â”€â” this `k2` is the lambda defined below
    k2.(x)       #  â”‚
  end).(fn a ->  # <â”˜
                 # the outer continuation receives the result of the
    k1.(- a)     # primitive function application
  end)
end
}

â—Šp{If you manually evaluate the above expression, you'll see that it is
indeed equivalent to the original expression.}

â—Šp{If there are multiple arguments, we first have to
â—Šemph{curry}â—Šfootnote{currying-wiki} the function before
converting. This is the default behavior in a few languages such as
Haskell and OCaml, but is a bit unusual in Elixir. If we start with
the following for
â—Šhyperlink["https://hexdocs.pm/elixir/Kernel.html#-/2"]{â—Šcode{Kernel.-/2}}:}

â—Šhighlight['elixir]{
fn x, y ->
  x - y
end
}

â—Šp{The curried form (not yet "â—Šemph{CPSed}"), is equivalent to:}

â—Šhighlight['elixir]{
fn x ->
  fn y ->
    x - y
  end
end
}

â—Šp{The rule for a 2-arity primitive application is:}

â—Šhighlight['racket]{
[[ p x y ]] = Î»Îº. [[ x ]] (Î»a. [[ y ]] (Î»b. Îº (p a b)))
}

â—Šp{This rule can be extended further for more arguments. Expressing this
in Elixir: â—Šspan[#:id "cps_prim_app_minus1"]{}}

â—Šhighlight['elixir]{
# assuming `x` and `y` are in scope here.
fn k1 ->
  (fn k2 ->
    k2.(x)
  end).(fn a ->
    (fn k3 ->
      k3.(y)
    end).(fn b ->
      k1.(a - b)
    end)
  end)
end
}

â—Šp{Pass this thing the "final continuation" (commonly the identity
function â—Šhyperlink["https://hexdocs.pm/elixir/Function.html#identity/1"]{â—Šcode{Function.identity/1}} or, more compactly, â—Šcode{& &1}), you should
see it results in â—Šcode{-1} as expected.}

â—Šp{A special case is that of 0-arity primitive functions. In that case,
we just invoke the function and pass it to the continuation, as if it
were a pure value.}

â—Šhighlight['elixir]{
node()
# ... becomes simply:
fn k -> k.(node()) end
}

â—Šh3[#:id "sec:lambdas"]{Lambdas}

â—Šp{The next case to consider is how to convert a lambda definition into
CPS. To do so, we make it accept a continuation as the first argument,
then immediately apply it to a lambda that takes the "original"
argument. The body of this inner lambda is another lambda that takes
another continuation, with the "CPSed" (converted into CPS) version of
the original lambda body fed this inner continuation.}

â—Šhighlight['racket]{
[[ Î»x. M ]] = Î»Îº1. Îº1 (Î»x. Î»Îº2. [[ M ]] Îº2)
}

â—Šp{Since that is quite convoluted, let's visualize it by considering the
identity function:}

â—Šhighlight['elixir]{
fn x -> x end
}

â—Šp{In CPS, it becomes:}

â—Šhighlight['elixir]{
fn k1 ->      # the outer continuation;
  k1.(fn x -> # the argument;
    fn k2 ->  # takes another continuation;
      # then we CPS the body of the original lambda and feed it k2.
      # [[ x ]] k2
    end
  end)
end
# â†“â†“â†“â†“â†“â†“â†“â†“â†“â†“â†“â†“â†“
fn k1 ->
  k1.(fn x ->
    fn k2 ->
      # since it is a irreducible value, we apply the same rules as
      # above.
      (fn k3 ->
        k3.(x)
      end).(k2)
    end
  end)
end
}

â—Šp{In the above example, one could
Î²-reduceâ—Šfootnote{lambda-reduction-wiki} the inner lambda and simplify
further. But I'll use this Î²-expanded version that generalizes better
for the cases belowâ—Šfootnote{paper-doubts} .}

â—Šp{There is an additional detail about the rule above: if a lambda like
the above is directly applied in code, as in â—Šcode{(fn x -> x end).(1)},
then the above conversion is the one use as the CPSed lambda to be
applied to â—Šcode{1} (as will be explored later). But if this lambda is
returned as a value, one must wrap it in another continuation layer as
if it were a pure value:}

â—Šhighlight['elixir]{
# the final version of our example, when returned as a value
fn k0 ->        # â† notice the extra continuation `k0`
  k0.(fn k1 ->  # â†
    k1.(fn x ->
      fn k2 ->
        (fn k3 ->
          k3.(x)
        end).(k2)
      end
    end)
  end)
end
}

â—Šp{I probably have messed something thing up when implementing, but I
needed to do this in order for all the thing to behave as
expected. I'm curious to know the correct version of this. =)}

â—Šp{In case of multiple arguments, we curry the function as in the
primitive function application case above before CPSing it with
similar rules. As an example that mixes lambda definitions and
primitive function applications in its body. The big highlighted area
is the CPSed version of the "minus one" shown â—Šhyperlink["#cps_prim_app_minus1"]{above}.}

â—Šhighlight['elixir]{
# [[ fn x, y -> x - y end ]]
#
fn k0 ->                        # â† that extra continuation layer
  k0.(fn k1 ->                  # â”€â”
    k1.(fn x ->                 # â”€â”˜ stuff for the `x` argument
      fn k3 ->
        k3.(fn y ->
          fn k4 ->
            (fn k1 ->           # â”€â”
              (fn k2 ->         #  â”‚
                k2.(x)          #  â”‚
              end).(fn a ->     #  â”‚ this is the CPSed version of
                (fn k3 ->       #  â”‚ the "minus one" function
                  k3.(y)        #  â”‚ shown above...
                end).(fn b ->   #  â”‚
                  k1.(a - b)    #  â”‚
                end)            #  â”‚
              end)              #  â”‚
            end).(k4)           # â”€â”˜ ... applied to the inner continuation
          end                   #    from the lambda
        end)
      end
    end)
  end)
end
}

â—Šp{This is already quite unwieldy, and anything more complicated tend to
grow quite fast in complexity. A good exercise is to take these more
basic examples and try to Î²-reduce them manually to get more intuition
of what-flows-where.}

â—Šp{We give the 0-arity case a slightly different treatment: we transform
the lambda body, wrap the result in a 0-arity lambda and return that
to a continuation.}

â—Šhighlight['elixir]{
fn -> :result end
# ... becomes:
fn k0 ->             # again, extra continuation layer
  k0.(fn k1 ->
    k1.(fn ->        # notice that there is no argument here
      fn k2 ->       # â”€â”
        k2.(:result) #  â”‚ lambda body converted
      end            # â”€â”˜
    end)
  end)
end
}

â—Šh3[#:id "sec:function-application"]{Function application}

â—Šp{The last type of terms I'll attempt to show here is the application of
functions to values. The implementation differentiates 3 sub-cases: i)
application of values to a lambda literal; ii) application to a named
lambda; iii) primitive function application. The last case was already
covered â—Šhyperlink["#sec:primitive-function-application"]{above}, and it is things of the form â—Šcode{fun(x)} and
â—Šcode{Node.ping()}. Case (i) is special because we use the converted lambda
version without the extra continuation layer. Finally, case (ii) is
treated specially because we assume that such function has already
been curried and CPSed, so we do not convert it further and simply
apply it using the rules that will be shown below.}

â—Šp{The conversion rule for an application is:}

â—Šhighlight['racket]{
[[ M N ]] = Î»Îº. [[ M ]] (Î»m. [[ N ]] (Î»n. m n Îº))
}

â—Šp{As a final example, we consider the application to a named lambda.}

â—Šhighlight['elixir]{
some_fun.(1)
# ... becomes
fn k1 ->
  some_fun.(fn m ->   # â† `some_fun` is considered already CPSed
    (fn k2 ->
      k2.(1)          # â† CPSed argument
    end).(fn n ->
      m.(n).(k1)
    end)
  end)
end
}

â—Šh3[#:id "sec:other-details"]{Other details}

â—Šp{For more details, I'll refer the reader to the implementation and to
some papers that describe the transformation â—Šfootnote{gh-lib-commit} â—Šfootnote{representing-control-danvy-filinski} â—Šfootnote{abstracting-control-danvy-filinski}.}

â—Šh2[#:id "sec:shift---reset"]{Shift / Reset}

â—Šp{Ok, that was quite a lot... Why go through all this trouble?}

â—Šp{The answer is that such transformations allow us to use some control
operators that are quite powerful. Some examples of applications that
can be implemented as libraries are: exceptions, backtracking search,
threads, generators and coroutines â—Šfootnote{matt-might-continuations}.}

â—Šp{Two of those operators are â—Šcode{shift} and â—Šcode{reset}, and there are a few
other more or less equivalent ones â—Šfootnote{racket-cont-doc}. They are most succinctly
conceptually described in the Racket documentation by the reduction
rules:}

â—Šhighlight['racket]{
;; "=>" means "reduces to"
(reset val) => val
(reset E[(shift k expr)]) => (reset (Î» (k) expr)
                                    (Î» (v) (reset E[v])))
;; where `E` has no `reset`
}

â—Šp{I have not found in that documentation what â—Šcode{E[_]} means. But, by
experimenting with the operators, it looks like it means the â—Šemph{dynamic
continuation enclosing} the call to â—Šcode{shift}, up to but not including
â—Šcode{reset}. So, in:}

â—Šhighlight['racket]{
(+ 1
   (reset
    (* 2
       (shift c
         (* 3 (c 4))))))
}

â—Šp{... â—Šcode{E[_]} means â—Šcode{(Î» (v) (* 2 v))}. Indeed, the expression above
evaluates to â—Šcode{25}. Using the second evaluation rule:}

â—Šhighlight['racket]{
(+ 1
   (reset
    ((Î» (k) (* 3 (k 4)))
     (Î» (v) (* 2 v)))))
;; Î²-reducing...
(+ 1
   (reset
    (* 3 ((Î» (v) (* 2 v)) 4))))
;; using the 1st reduction rule for `reset`...
(+ 1 (* 3 ((Î» (v) (* 2 v)) 4)))
;; Î²-reducing...
(+ 1 (* 3 (* 2 4)))
;; which yields 25
}

â—Šp{So, shortly, â—Šcode{shift} captures the continuation and binds it to be used
possibly multiple times. The extent of what is capture is determined
by the presence of â—Šcode{reset}, which acts as a delimiter (hence the name
â—Šemph{delimited continuations}).}

â—Šp{Just to illustrate, the example above could be written in Elixir as:}

â—Šhighlight['elixir]{
x = reset do
      2 * shift cont do
            3 * cont(4)
          end
    end
1 + x
}

â—Šp{I used an intermediate variable just to emphasize that the "remaining
lines" after an expression are continuations for it.}

â—Šp{I'll try to borrow the visualization ideas â—Šhyperlink["#ref_ghc_primops_alexis"]{from} â—Šhyperlink["#ref_guile_wingo"]{those} â—Šhyperlink["#ref_stackoverflow_alexis"]{references} and
attempt to illustrate conceptually how these operators are working in
this example (it is almost certainly wrong concretely, I don't know
how Elixir/Erlang break up stack frames). In the images below, each
rectangle is a continuation frame, and â— are the places where the
redexes go into after being reduced.}

â—Šfigure{â—Šimg[#:class "db w-60 center" #:src "../images/elixir-delimited-continuations-stack1.png" #:id "stack1"]{}}

â—Šp{When â—Šcode{shift} is invoked, it essentially captures the frames from the
current one up to the nearest enclosing â—Šcode{reset}, packages those up in
â—Šcode{cont}, and replaces them with its own body frames.}

â—Šfigure{â—Šimg[#:class "db w-60 center" #:src "../images/elixir-delimited-continuations-stack2.png" #:id "stack2"]{}}

â—Šp{Which reduces to â—Šcode{25}, as before.}

â—Šh1[#:id "sec:footnotes"]{Footnotes}

â—Šhr{}

â—Šol{
â—Šdeffn['call-cc-wiki]{
â—Šhyperlink["https://en.m.wikibooks.org/wiki/Haskell/Continuation_passing_style#callCC"]{https://en.m.wikibooks.org/wiki/Haskell/Continuation_passing_style#callCC}
}

â—Šdeffn['scheme-call-cc-wiki]{
â—Šhyperlink["http://community.schemewiki.org/?call-with-current-continuation"]{http://community.schemewiki.org/?call-with-current-continuation}
}

â—Šdeffn['continuation-study-group-gh]{
â—Šhyperlink["https://github.com/rain-1/continuations-study-group"]{https://github.com/rain-1/continuations-study-group}
}

â—Šdeffn['campinas-name]{
It is the name of a â—Šhyperlink["https://en.wikipedia.org/wiki/Campinas"]{city} whose contraction is â—Šemph{CPS}.
}

â—Šdeffn['abstracting-control-danvy-filinski]{
â—Šhyperlink["https://citeseerx.ist.psu.edu/viewdoc/summary?doi=10.1.1.43.8753"]{Olivier Danvy and Andre Filinski, â€œAbstracting Control,â€ LISP and Functional Programming, 1990}
}

â—Šdeffn['ghc-continuation-primops]{
â—Šhyperlink["https://github.com/ghc-proposals/ghc-proposals/blob/master/proposals/0313-delimited-continuation-primops.rst"]{https://github.com/ghc-proposals/ghc-proposals/blob/master/proposals/0313-delimited-continuation-primops.rst}
}

â—Šdeffn['algebraic-effects-handlers]{
â—Šhyperlink["https://doi.org/10.1016/j.entcs.2015.12.003"]{https://doi.org/10.1016/j.entcs.2015.12.003}
}

â—Šdeffn['racket-cont-model]{
â—Šhyperlink["https://docs.racket-lang.org/reference/eval-model.html#%28part._cont-model%29"]{https://docs.racket-lang.org/reference/eval-model.html#%28part._cont-model%29}
}

â—Šdeffn['representing-control-danvy-filinski]{
â—Šhyperlink["https://www.cambridge.org/core/journals/mathematical-structures-in-computer-science/article/abs/representing-control-a-study-of-the-cps-transformation/37193FD94F87443338FC7F519783FF0A"]{Olivier Danvy and Andre Filinski, "Representing Control: a Study of the CPS Transformation", MSCS, 1992}
}

â—Šdeffn['currying-wiki]{
â—Šhyperlink["https://en.wikipedia.org/wiki/Currying"]{https://en.wikipedia.org/wiki/Currying}
}

â—Šdeffn['lambda-reduction-wiki]{
â—Šhyperlink["https://en.wikipedia.org/wiki/Lambda_calculus#Reduction"]{https://en.wikipedia.org/wiki/Lambda_calculus#Reduction}
}

â—Šdeffn['paper-doubts]{
It is also one point that I could
not understand quite well when reading the papers. I needed to do this
for the implementation to work properly for my test cases, but the
equations in â—Šreference-footnote{representing-control-danvy-filinski} are somewhat different.
}

â—Šdeffn['gh-lib-commit]{
â—Šhyperlink["https://github.com/thalesmg/campinas/tree/2d41f2697395c2fc542500ad32c62f0f307c7220"]{https://github.com/thalesmg/campinas/tree/2d41f2697395c2fc542500ad32c62f0f307c7220}
}

â—Šdeffn['matt-might-continuations]{
â—Šhyperlink["https://matt.might.net/articles/programming-with-continuations--exceptions-backtracking-search-threads-generators-coroutines/"]{"Continuations by example: Exceptions, time-traveling search, generators, threads, and coroutines", by Matt Might}
}

â—Šdeffn['racket-cont-doc]{
â—Šhyperlink["https://docs.racket-lang.org/reference/cont.html"]{https://docs.racket-lang.org/reference/cont.html}
}
}
